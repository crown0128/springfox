apply from: "$rootDir/gradle/dependencies.gradle"

buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath "com.github.adrianbk:gradle-jvmsrc-plugin:0.6.1"
    classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.0"
    classpath "com.github.adrianbk:gradle-travisci-trigger-plugin:1.0.0"
    classpath 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    classpath "com.ofg:uptodate-gradle-plugin:1.1.0"
  }
}

ext {
  apiKey = System.getenv('GIT_HUB_API_KEY')
  projectVersion = '2.0.0-SNAPSHOT'
  jdkVersion = 1.6
}

configure(publishable(subprojects)) { subproject ->
  apply plugin: 'groovy'
  apply plugin: 'java'
  apply plugin: "com.ofg.uptodate"
  apply plugin: 'maven'
  apply plugin: 'idea'
  ext {
    jdkVersion = 1.6
  }
  compileJava.options.encoding = 'UTF-8'

  sourceCompatibility = jdkVersion
  targetCompatibility = jdkVersion

  //Adds provide dependency configuration
  configurations {
    provided
    compile.extendsFrom provided
  }

  group = 'com.mangofactory.documentation'
  version = projectVersion

  apply plugin: 'com.github.adrianbk.jvmsrc'
  jvmsrc {
    packageName "${group}"
  }

  jar {
    manifest {
      attributes(
              'Implementation-Title': "${project.name}",
              'Implementation-Version': version.toString(),
              'Created-By': System.getProperty('java.version') + ' (' + System.getProperty('java.vendor') + ')',
              'Built-With': "gradle-${project.getGradle().getGradleVersion()}, groovy-${GroovySystem.getVersion()}",
              'Build-Time': "${new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")}",
              'Built-By': System.getProperty('user.name'),
              'Built-On': "${InetAddress.localHost.hostName}/${InetAddress.localHost.hostAddress}"
      )
    }
  }

  apply plugin: 'checkstyle'
  checkstyle {
    configFile = file("$rootDir/config/checkstyle.xml")
  }

  checkstyleMain.source = "src/main/java"
  apply plugin: 'maven-publish'
  apply from: "$rootDir/gradle/publishing.gradle"
  apply from: "$rootDir/gradle/artifacts.gradle"
  apply from: "$rootDir/gradle/coverage.gradle"
  apply from: "$rootDir/gradle/code-quality.gradle"

  repositories {
    jcenter()
  }

}

def publishable(subprojects) {
  subprojects.findAll {
    it.name != 'swagger-contract-tests' &&
    it.name != 'springdox-petstore'
  }
}

apply plugin: "com.github.adrianbk.tcitrigger"

tciTrigger {
  gitHubRepo 'adrianbk/swagger-springmvc-demo'
  gitHubApiKeyVarName = 'GIT_HUB_API_KEY'
  travisVariable {
    name = "LATEST_SSMVC_VERSION"
    value = "${projectVersion}"
    visible = true
  }
}

task wrapper(type: Wrapper) {}
