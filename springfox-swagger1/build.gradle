plugins {
  id "net.ltgt.apt-idea" version "0.21"
}

ext {
  limits = [
      'instruction': 97,
      'branch'     : 95,
      'line'       : 97,
      'complexity' : 94,
      'method'     : 94,
      'class'      : 95.5
  ]
}

sourceSets {
  integrationTest { sourceSet ->
    ["java", "groovy", "scala", "resources"].each {
      if (!sourceSet.hasProperty(it)) return
      sourceSet."$it".srcDir file("src/integration-test/${it}")
    }
  }
}


// Define integration test task
task integrationTest(type: Test) {
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
}

dependencies {
  api project(':springfox-spi')
  api project(':springfox-schema')
  api project(':springfox-core')
  api project(':springfox-spring-web')
  api project(':springfox-spring-webmvc')
  api project(':springfox-swagger-common')

  api libs.core
  api libs.spring
  api libs.swagger2Core
  compileOnly libs.springProvidedWithMvc
  compileOnly libs.clientProvided
  compileOnly "javax.servlet:javax.servlet-api:$servlet"

  implementation "org.mapstruct:mapstruct:$mapstruct"
  annotationProcessor "org.mapstruct:mapstruct-processor:$mapstruct"

  testImplementation project(':springfox-core').sourceSets.test.output
  testImplementation project(':springfox-schema').sourceSets.test.output
  testImplementation project(':springfox-swagger-common').sourceSets.test.output
  testImplementation project(':springfox-spring-web').sourceSets.test.output
  testImplementation project(':springfox-spring-webmvc').sourceSets.test.output
  testImplementation libs.test
  testImplementation libs.springProvidedWithMvc
  testImplementation libs.clientProvided
  testImplementation "javax.servlet:javax.servlet-api:$servlet"

  integrationTestCompile sourceSets.main.output
  integrationTestCompile sourceSets.test.output
  integrationTestCompile configurations.testImplementation
  integrationTestRuntime configurations.testRuntime
}

compileJava {
  options.compilerArgs += [
          "-Amapstruct.defaultComponentModel=spring",
          "-Amapstruct.unmappedTargetPolicy=ERROR"
          ]
}